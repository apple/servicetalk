/*
 * Copyright Â© 2019 Apple Inc. and the ServiceTalk project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
  if (!repositories) {
    repositories {
      jcenter()
      maven { url "https://plugins.gradle.org/m2/" }
    }
  }

  dependencies {
    classpath "io.servicetalk:servicetalk-gradle-plugin-internal:$project.version"
    // TODO find a way to share this version with servicetalk-grpc-gradle-plugin/build.gradle
    classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.10"
  }
}

apply plugin: "servicetalk-library"
apply plugin: "com.google.protobuf"

dependencies {
  implementation "io.servicetalk:servicetalk-bom-internal:$project.version"

  api "io.servicetalk:servicetalk-grpc-api:$project.version"
  api "com.google.protobuf:protobuf-java"
  api "com.google.api.grpc:proto-google-common-protos"

  implementation "io.servicetalk:servicetalk-annotations:$project.version"
  implementation "io.servicetalk:servicetalk-buffer-netty:$project.version"
  implementation "org.slf4j:slf4j-api"
  implementation "com.google.code.findbugs:jsr305"

  testImplementation "io.servicetalk:servicetalk-test-resources:$project.version"
  testImplementation "junit:junit"
  testImplementation "org.hamcrest:hamcrest-library"
  testImplementation "org.mockito:mockito-core"
}

// Versions for managed dependencies are not available as properties so we retrieve it from a concrete dependency
def protobufVersion = configurations.compileClasspath.resolvedConfiguration.firstLevelModuleDependencies
    .findAll { it.moduleName == "protobuf-java" }[0].moduleVersion

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:$protobufVersion"
  }
}

// Add the generated code used for tests to the source set.
sourceSets {
  test {
    resources {
      srcDir "${protobuf.generatedFilesBaseDir}/source/test/java"
    }
  }

}

clean {
  delete protobuf.generatedFilesBaseDir
}
