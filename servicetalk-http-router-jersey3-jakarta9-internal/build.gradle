/*
 * Copyright Â© 2019, 2024 Apple Inc. and the ServiceTalk project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "io.servicetalk.servicetalk-gradle-plugin-internal-library"

// Required version for module
def javaLanguageVersion = JavaVersion.VERSION_11

if (!JavaVersion.current().isCompatibleWith(javaLanguageVersion)) {
  project.tasks.all { task -> task.enabled = false }
}

java {
  sourceCompatibility = javaLanguageVersion
  targetCompatibility = javaLanguageVersion
}

compileJava {
  options.release = Integer.parseInt(javaLanguageVersion.getMajorVersion())
}

compileTestJava {
  options.release = compileJava.options.release
}

tasks.withType(Checkstyle).all {
  enabled false
}

tasks.withType(Pmd).all {
  enabled false
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).all {
  enabled false
}

task cleanSources(type: Delete) {
  delete 'src'
}

task copySourcesForJersey3(type: Copy) {
  dependsOn tasks.cleanSources
  from '../servicetalk-http-router-jersey-internal/src'
  into 'src'
  filter { line -> line.replaceAll('javax.ws.rs', 'jakarta.ws.rs') }
  filter { line -> line.replaceAll('javax.inject', 'jakarta.inject') }
}

tasks.processResources.dependsOn(copySourcesForJersey3)
tasks.sourcesJar.dependsOn(copySourcesForJersey3)
tasks.compileJava.dependsOn(copySourcesForJersey3)

def actualJerseyVersion = "${jersey3VersionEE9}"
def actualJavaxActivationVersion = "${javaxActivationVersionEE9}"
def actualJavaxJaxbApiVersion = "${javaxJaxbApiVersionEE9}"
def actualJavaxJaxbCoreVersion = "${javaxJaxbCoreVersionEE9}"
def actualJavaxJaxbImplVersion = "${javaxJaxbImplVersionEE9}"
def actualJaxRsVersion = "${jaxRsVersionEE9}"

// Dependencies must remain consistent between servicetalk-http-router-jerseyX-internal modules
dependencies {
  api platform(project(":servicetalk-dependencies"))
  api platform("org.glassfish.jersey:jersey-bom:${actualJerseyVersion}")
  api "jakarta.activation:jakarta.activation-api:$actualJavaxActivationVersion"
  api "org.glassfish.jaxb:jaxb-core:$actualJavaxJaxbCoreVersion"
  api "jakarta.ws.rs:jakarta.ws.rs-api:$actualJaxRsVersion"
  api "jakarta.xml.bind:jakarta.xml.bind-api:$actualJavaxJaxbApiVersion"
  api "org.glassfish.jaxb:jaxb-runtime:$actualJavaxJaxbImplVersion"

  api project(":servicetalk-http-api")
  api "jakarta.ws.rs:jakarta.ws.rs-api"

  implementation project(":servicetalk-annotations")
  implementation project(":servicetalk-concurrent-api-internal")
  implementation project(":servicetalk-concurrent-internal")
  implementation project(":servicetalk-http-utils")
  implementation "com.google.code.findbugs:jsr305"
  implementation "org.glassfish.jersey.core:jersey-common"
}
