/*
 * Copyright Â© 2019, 2021, 2024 Apple Inc. and the ServiceTalk project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "io.servicetalk.servicetalk-gradle-plugin-internal-library"

// Required version for module
def javaLanguageVersion = JavaVersion.VERSION_11

if (!JavaVersion.current().isCompatibleWith(javaLanguageVersion)) {
  project.tasks.all { task -> task.enabled = false }
}

java {
  sourceCompatibility = javaLanguageVersion
  targetCompatibility = javaLanguageVersion
}

compileJava {
  options.release = Integer.parseInt(javaLanguageVersion.getMajorVersion())
}

compileTestJava {
  options.release = compileJava.options.release
}

tasks.withType(Checkstyle).all {
  enabled false
}

tasks.withType(Pmd).all {
  enabled false
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).all {
  enabled false
}

task cleanSources(type: Delete) {
  // Don't delete src/testFixtures/java/.gitkeep
  // gitkeep used to ensure that a src/textFixtures/java exists or Gradle processing will fail
  delete 'src/main', 'src/test', 'src/testFixtures/java/io'
}

task copySourcesForJersey3(type: Copy) {
  dependsOn tasks.cleanSources
  from '../servicetalk-http-router-jersey/src'
  into 'src'
  filter { line -> line.replaceAll('javax.ws.rs', 'jakarta.ws.rs') }
  filter { line -> line.replaceAll('javax.inject', 'jakarta.inject') }

  // Only modify specific javax.annotations.  Some like javax.annotation.Nullable need to stay the same
  filter { line -> line.replaceAll('javax.annotation.Priority', 'jakarta.annotation.Priority') }
}

tasks.processResources.dependsOn(copySourcesForJersey3)
tasks.sourcesJar.dependsOn(copySourcesForJersey3)
tasks.compileJava.dependsOn(copySourcesForJersey3)
tasks.processTestResources.dependsOn(copySourcesForJersey3)
tasks.compileTestJava.dependsOn(copySourcesForJersey3)

def actualJerseyVersion = "${jersey3VersionEE10}"
def actualJavaxActivationVersion = "${javaxActivationVersionEE10}"
def actualJavaxJaxbApiVersion = "${javaxJaxbApiVersionEE10}"
def actualJavaxJaxbCoreVersion = "${javaxJaxbCoreVersionEE10}"
def actualJavaxJaxbImplVersion = "${javaxJaxbImplVersionEE10}"
def actualJaxRsVersion = "${jaxRsVersionEE10}"

// Dependencies must remain consistent between servicetalk-http-router-jerseyX modules
dependencies {
  api platform(project(":servicetalk-dependencies"))
  api platform("org.glassfish.jersey:jersey-bom:${actualJerseyVersion}")
  api "jakarta.activation:jakarta.activation-api:$actualJavaxActivationVersion"
  api "org.glassfish.jaxb:jaxb-core:$actualJavaxJaxbCoreVersion"
  api "jakarta.ws.rs:jakarta.ws.rs-api:$actualJaxRsVersion"
  api "jakarta.xml.bind:jakarta.xml.bind-api:$actualJavaxJaxbApiVersion"
  api "org.glassfish.jaxb:jaxb-runtime:$actualJavaxJaxbImplVersion"
  testImplementation enforcedPlatform("org.junit:junit-bom:$junit5Version")
  testFixturesImplementation platform(project(":servicetalk-dependencies"))
  testFixturesImplementation platform("org.junit:junit-bom:$junit5Version")

  api project(":servicetalk-http-api")
  api project(":servicetalk-router-api")
  api "jakarta.ws.rs:jakarta.ws.rs-api"

  implementation project(":servicetalk-annotations")
  implementation project(":servicetalk-concurrent-api-internal")
  implementation project(":servicetalk-concurrent-internal")
  implementation project(":servicetalk-data-jackson")
  implementation project(":servicetalk-http-router-jersey3-jakarta10-internal")
  implementation project(":servicetalk-http-utils")
  implementation project(":servicetalk-router-utils-internal")
  implementation project(":servicetalk-utils-internal")
  implementation "com.google.code.findbugs:jsr305"
  implementation "com.sun.activation:jakarta.activation"
  implementation "com.sun.xml.bind:jaxb-core"
  implementation "jakarta.xml.bind:jakarta.xml.bind-api"
  implementation "org.glassfish.jersey.core:jersey-server"
  implementation "org.slf4j:slf4j-api"

  runtimeOnly "com.sun.xml.bind:jaxb-impl"

  testImplementation "org.junit.jupiter:junit-jupiter-api"
  testImplementation "org.mockito:mockito-core:$mockitoCoreVersion"

  testRuntimeOnly "org.glassfish.jersey.media:jersey-media-json-jackson"

  testFixturesImplementation testFixtures(project(":servicetalk-concurrent-api"))
  testFixturesImplementation testFixtures(project(":servicetalk-concurrent-internal"))
  testFixturesImplementation testFixtures(project(":servicetalk-http-netty"))
  testFixturesImplementation testFixtures(project(":servicetalk-transport-netty-internal"))
  testFixturesImplementation project(":servicetalk-data-jackson")
  testFixturesImplementation project(":servicetalk-http-netty")
  testFixturesImplementation project(":servicetalk-http-utils")
  testFixturesImplementation project(":servicetalk-http-router-predicate")
  testFixturesImplementation project(":servicetalk-router-utils-internal")
  testFixturesImplementation project(":servicetalk-test-resources")
  testFixturesImplementation project(":servicetalk-transport-netty")
  testFixturesImplementation project(":servicetalk-transport-netty-internal")
  testFixturesImplementation "com.google.code.findbugs:jsr305:$jsr305Version"
  testFixturesImplementation "org.junit.jupiter:junit-jupiter-api"
  testFixturesImplementation "org.junit.jupiter:junit-jupiter-params"
  testFixturesImplementation "org.junit.platform:junit-platform-suite:$junitPlatformVersion"
  testFixturesImplementation "net.javacrumbs.json-unit:json-unit:$jsonUnitVersion"
  testFixturesImplementation "org.glassfish.jersey.inject:jersey-hk2"
  testFixturesImplementation "org.glassfish.jersey.media:jersey-media-sse"
  testFixturesImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
  testFixturesImplementation "org.mockito:mockito-core:$mockitoCoreVersion"
  testFixturesImplementation "org.mockito:mockito-junit-jupiter:$mockitoCoreVersion"
}
