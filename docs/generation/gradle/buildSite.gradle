/*
 * Copyright Â© 2019 Apple Inc. and the ServiceTalk project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
task buildLocalSite(type: NodeTask) { task ->
  dependsOn installAntora

  def outputDir = file("$buildDir/local")

  def servicetalkRoot = "../../"

  inputs.file("site-local.yml")
  inputs.dir("$servicetalkRoot/docs")
  inputs.dir("$servicetalkRoot/servicetalk-examples/docs")

  outputs.dir(outputDir)

  script = file('./node_modules/.bin/antora')
  args = [
      "--stacktrace",
      "--attribute", isReleaseBuild ? "is-release-version=defined" : "sourceroot=file://${file(servicetalkRoot)}/",
      "site-local.yml",
  ]

  // workaround for https://gitlab.com/antora/antora/issues/390
  execOverrides {
    it.environment "NODE_TLS_REJECT_UNAUTHORIZED", 0
  }

  doLast {
    println("Generated local site at: file://$outputDir/index.html")
  }
}

task buildRemoteSite(type: NodeTask) { task ->
  dependsOn installAntora

  def outputDir = file("$buildDir/remote")

  inputs.file("site-remote.yml")
  outputs.dir(outputDir)

  if (project.ext.isCiBuild) {
    execOverrides {
      it.environment "GIT_CREDENTIALS": "https://${file(System.getProperty("antoraKeyPath")).text}:@github.pie.apple.com"
    }
  }

  script = file('./node_modules/.bin/antora')
  args = [
      "--stacktrace",
      "--attribute", isReleaseBuild ? "is-release-version=defined" : "is-snapshot-version=defined",
      "site-remote.yml"
  ]

  // workaround for https://gitlab.com/antora/antora/issues/390
  execOverrides {
    it.environment "NODE_TLS_REJECT_UNAUTHORIZED", 0
  }

  doLast {
    println("Generated remote site at: file://$outputDir/index.html")
  }
}

task publishRemoteSite(type: Exec) {
  // important to run this as a self-contained CI step with GitHub deploy key,
  // hence no task dependency on `buildRemoteSite`

  executable = "bash"

  args = [
      "-c",
      """
      set -xe

      BRANCH_NAME=\$(git symbolic-ref -q HEAD)
      BRANCH_NAME=\${BRANCH_NAME##refs/heads/}
      GIT_AUTHOR=\$(git --no-pager show -s --format='%an <%ae>' HEAD)
      if ( ! git remote get-url docs ); then
        git remote add docs git@github.pie.apple.com:servicetalk/servicetalk.git
      fi
      git fetch docs +gh-pages:gh-pages
      git worktree add gh-pages gh-pages
      cd gh-pages

      rm -rf *
      touch .nojekyll
      cp -r ../.out/remote/* .
      git add * .nojekyll

      git commit --author="\$GIT_AUTHOR" -m "Publish docsite"
      git push docs gh-pages

      cd ..
      rm -rf gh-pages
      git worktree prune
    """.stripIndent()
  ]
}
