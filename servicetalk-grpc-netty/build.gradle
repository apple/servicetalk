/*
 * Copyright Â© 2019, 2021 Apple Inc. and the ServiceTalk project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  dependencies {
    classpath "com.google.protobuf:protobuf-gradle-plugin:$protobufGradlePluginVersion"
  }
}

apply plugin: "io.servicetalk.servicetalk-gradle-plugin-internal-library"
apply plugin: "com.google.protobuf"

dependencies {
  api project(":servicetalk-grpc-api")
  api project(":servicetalk-http-netty")

  implementation project(":servicetalk-http-utils")
  implementation project(":servicetalk-grpc-internal")
  implementation project(":servicetalk-annotations")
  implementation project(":servicetalk-transport-netty-internal")
  implementation project(":servicetalk-utils-internal")
  implementation "org.slf4j:slf4j-api:$slf4jVersion"
  implementation "com.google.code.findbugs:jsr305:$jsr305Version"

  testImplementation testFixtures(project(":servicetalk-concurrent-api"))
  testImplementation testFixtures(project(":servicetalk-concurrent-internal"))
  testImplementation testFixtures(project(":servicetalk-transport-netty-internal"))
  testImplementation testFixtures(project(":servicetalk-buffer-api"))
  testImplementation testFixtures(project(":servicetalk-http-netty"))
  testImplementation project(":servicetalk-concurrent-api-internal")
  testImplementation project(":servicetalk-concurrent-test-internal")
  testImplementation project(":servicetalk-encoding-api-internal")
  testImplementation project(":servicetalk-encoding-netty")
  testImplementation project(":servicetalk-grpc-protobuf")
  testImplementation project(":servicetalk-grpc-protoc")
  testImplementation project(":servicetalk-router-utils-internal")
  testImplementation project(":servicetalk-test-resources")
  testImplementation project(":servicetalk-utils-internal")
  testImplementation project(":servicetalk-transport-netty")
  testImplementation "io.grpc:grpc-core:$grpcVersion"
  testImplementation("io.grpc:grpc-netty:$grpcVersion") {
    exclude group: "io.netty"
  }
  testImplementation "io.grpc:grpc-protobuf:$grpcVersion"
  testImplementation "io.grpc:grpc-stub:$grpcVersion"
  testImplementation "io.netty:netty-tcnative-boringssl-static:$tcnativeVersion"
  testImplementation "jakarta.annotation:jakarta.annotation-api:$javaxAnnotationsApiVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junit5Version"
  testImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
  testImplementation "org.mockito:mockito-core:$mockitoCoreVersion"

  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:$protobufVersion"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
    }
    servicetalk_grpc {
      //// Users are expected to use "artifact" instead of "path". we use "path"
      //// only because we want to use the gradle project local version of the plugin
      path = file("${project.rootProject.rootDir}/servicetalk-grpc-protoc/build" +
                  "/buildExecutable/servicetalk-grpc-protoc-${project.version}-all.jar").path
    }
  }
  generateProtoTasks {
    all().each { task ->
      task.plugins {
        grpc {}
        servicetalk_grpc {
          outputSubDir = "java"
        }
      }
    }
  }
  generatedFilesBaseDir = "$buildDir/generated/sources/proto"
}

//// The following setting must be omitted in users projects and is necessary here
//// only because we want to use the gradle project local version of the plugin
afterEvaluate {
  generateTestProto.dependsOn(":servicetalk-grpc-protoc:buildExecutable")
}
