/*
 * Copyright Â© 2019 Apple Inc. and the ServiceTalk project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
  if (!repositories) {
    repositories {
      jcenter()
      maven { url "https://plugins.gradle.org/m2/" }
    }
  }

  dependencies {
    classpath "io.servicetalk:servicetalk-gradle-plugin-internal:$project.version"
    classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    // TODO find a way to share this version with servicetalk-grpc-gradle-plugin/build.gradle
    classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.10"
  }
}

apply plugin: "servicetalk-library"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "com.google.protobuf"

dependencies {
  implementation "io.servicetalk:servicetalk-bom-internal:$project.version"

  implementation "com.google.code.findbugs:jsr305"
  implementation "com.google.protobuf:protobuf-java"
  implementation "com.squareup:javapoet"
  implementation "io.servicetalk:servicetalk-annotations:$project.version"

  testImplementation "io.servicetalk:servicetalk-grpc-api:$project.version"
  testImplementation "io.servicetalk:servicetalk-grpc-protobuf:$project.version"
}

jar {
  manifest {
    attributes "Implementation-Title": "ServiceTalk gRPC Protoc Plugin",
        "Implementation-Version": project.version,
        "Main-Class": "io.servicetalk.grpc.protoc.Main"
  }
}

shadowJar {
  baseName = project.name + "-all"
  classifier = ''
}

task buildExecutable {
  def outputFile = file("$buildDir/buildExecutable/protoc-gen-servicetalk_grpc.exe")
  inputs.files shadowJar.outputs.files
  outputs.file outputFile

  doLast {
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
      throw new GradleException("Windows is currently not supported")
    }

    outputFile.parentFile.mkdirs()
    outputFile.delete()
    outputFile <<
        '''#!/bin/sh
exec java -jar "$0" "$@"
exit1
'''
    shadowJar.outputs.files.singleFile.withInputStream { outputFile.append(it) }
    outputFile.executable = true
  }
}
test.dependsOn buildExecutable
assemble.dependsOn buildExecutable
ideaModule.dependsOn buildExecutable

publishing {
  publications {
    mavenJava {
      artifact(buildExecutable.outputs.files.singleFile) {
        classifier = "linux-x86_64"
        extension = "exe"
        builtBy buildExecutable
      }
      artifact(buildExecutable.outputs.files.singleFile) {
        classifier = "osx-x86_64"
        extension = "exe"
        builtBy buildExecutable
      }
    }
  }
}

// Versions for managed dependencies are not available as properties so we retrieve it from a concrete dependency
def protobufVersion = configurations.compileClasspath.resolvedConfiguration.firstLevelModuleDependencies
    .findAll { it.moduleName == "protobuf-java" }[0].moduleVersion

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:$protobufVersion"
  }

  plugins {
    servicetalk_grpc {
      path = "$buildDir/buildExecutable/protoc-gen-servicetalk_grpc.exe"
    }
  }

  // We validate that our protoc plugin outputs valid code by generating test classes which are compiled by Gradle
  generateProtoTasks {
    ofSourceSet("test").each { task ->
      task.plugins {
        servicetalk_grpc {
          outputSubDir = "java"
        }
      }
    }
  }
}

clean {
  delete protobuf.generatedFilesBaseDir
}
