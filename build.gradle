/*
 * Copyright Â© 2018-2019 Apple Inc. and the ServiceTalk project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import io.servicetalk.gradle.plugin.internal.Versions

if (!repositories) {
  allprojects {
    buildscript {
      repositories {
        gradlePluginPortal()
      }
    }
    repositories {
      mavenCentral()
      gradlePluginPortal()
      if ("$nettyVersion".endsWith("+")) {
        maven {
          name "Netty Snapshots"
          url "https://central.sonatype.com/repository/maven-snapshots/"

          content {
            includeGroup "io.netty"
          }
        }
      }
    }
  }
  println("This machine doesn't have any pre-configured repositories, will use: ${repositories*.name.join(", ")}")
} else {
  print("This machine already has some pre-configured repositories, will use: ${repositories*.name.join(", ")}")
}

buildscript {
  if (!repositories) {
    repositories {
      gradlePluginPortal()
    }
  }
  dependencies {
    if (JavaVersion.current().isJava11Compatible()) {
      classpath("com.autonomousapps:dependency-analysis-gradle-plugin:$dependencyAnalysisPluginVersion")
    }
  }
}

apply plugin: "io.servicetalk.servicetalk-gradle-plugin-internal-root"

// No default toolchain configuration at root level
// Main sources compile with the JDK running Gradle (typically JDK 17 in CI)
// Test sources compile and run with TEST_JAVA_VERSION if set, otherwise use current JDK
// Modules requiring specific JDKs (jdkflow, Jersey) override this for their main sources

// Unfortunately, we can not make this validation part of our servicetalk-gradle-plugin-internal because it requires
// JDK11 while we build and package our plugin with JDK8.
if (JavaVersion.current().isJava11Compatible()) {
  apply plugin: "com.autonomousapps.dependency-analysis"

  dependencyAnalysis {
    issues {
      all {
        onAny {
          severity("fail")
        }
        onUnusedDependencies {
          // We use it for log4j2.xml configuration
          exclude(":servicetalk-test-resources")
        }
        onUsedTransitiveDependencies {
          // We import it as api dependency via :servicetalk-annotations and don't want to repeat it explicitly
          exclude("com.google.code.findbugs:jsr305")
        }
        onIncorrectConfiguration {
          // We don't want to leak it as api dependency to our users
          exclude("com.google.code.findbugs:jsr305")
        }
        onCompileOnly {
          // False positive, these annotations use RetentionPolicy.RUNTIME
          exclude(":servicetalk-annotations")
        }
      }
    }
  }

  quality.dependsOn buildHealth
}

task validateLocalDocSite(type: Exec) {
  group = 'Documentation'
  description = 'Generate and validate servicetalk.io site documentation'
  workingDir 'docs/generation'
  commandLine './gradlew', 'clean', 'validateLocalSite'
}
quality.dependsOn validateLocalDocSite

subprojects {
  if (JavaVersion.current() >= JavaVersion.VERSION_11) {
    apply plugin: "com.autonomousapps.dependency-analysis"
  }
  // Used by ci-release.yaml to determine which modules need to be built/released with JDK11.
  task printJavaTargetCompatibility {
    doLast {
      if (project.parent == project.rootProject) {
        def javaLanguage = (project.tasks.withType(JavaCompile)?.findByName("compileJava")?.options?.release?.get()) ?:
            Integer.parseInt(Versions.TARGET_VERSION.getMajorVersion())
        println("version: " + javaLanguage + " name: " + project.name)
      }
    }
  }

  // Debug task to verify which JDK is actually being used for compilation and testing
  // Must run after evaluation to capture final task configuration
  afterEvaluate {
    task verifyJdkUsage {
      doLast {
        if (project.parent == project.rootProject) {
          println "=" * 80
          println "Project: $project.name"
          println "Gradle Daemon JVM: ${org.gradle.internal.jvm.Jvm.current().javaHome}"
          println "Gradle Daemon JVM Version: ${JavaVersion.current()}"

          // Show configured toolchain if available
          if (project.hasProperty('java') && project.java.hasProperty('toolchain')) {
            def toolchain = project.java.toolchain
            if (toolchain.languageVersion.present) {
              println "Configured Toolchain Version: ${toolchain.languageVersion.get()}"
            } else {
              println "Configured Toolchain Version: not set (will use Gradle daemon JVM)"
            }
          }

          tasks.withType(JavaCompile).each { compileTask ->
            println "\nCompile Task: ${compileTask.name}"
            println "  Source Compatibility: ${compileTask.sourceCompatibility}"
            println "  Target Compatibility: ${compileTask.targetCompatibility}"

            // Show actual JDK being used for compilation (from toolchain)
            if (compileTask.hasProperty('javaCompiler') && compileTask.javaCompiler.present) {
              def compiler = compileTask.javaCompiler.get()
              println "  Compiler JDK: ${compiler.metadata.installationPath}"
              println "  Compiler Version: ${compiler.metadata.languageVersion}"
            } else {
              println "  Compiler JDK: using Gradle daemon JVM"
            }

            if (compileTask.options.release.isPresent()) {
              println "  Release Flag: ${compileTask.options.release.get()}"
            } else {
              println "  Release Flag: not set"
            }
          }

          tasks.withType(Test).each { testTask ->
            println "\nTest Task: ${testTask.name}"

            // Show actual JDK being used for tests (from toolchain)
            if (testTask.hasProperty('javaLauncher') && testTask.javaLauncher.present) {
              def launcher = testTask.javaLauncher.get()
              println "  Test JDK: ${launcher.metadata.installationPath}"
              println "  Test JDK Version: ${launcher.metadata.languageVersion}"
            } else {
              println "  Test JDK: using Gradle daemon JVM"
              println "  Test JDK Version: ${JavaVersion.current()}"
            }
          }
          println "=" * 80
        }
      }
    }
  }

  // Configure dependency locking: https://docs.gradle.org/current/userguide/dependency_locking.html
  dependencyLocking {
    lockMode = LockMode.STRICT
  }

  def lockableConfigs = ["annotationProcessor", "compileClasspath", "runtimeClasspath", "jmhCompileClasspath",
                         "jmhRuntimeClasspath"]
  configurations.configureEach {
    if (lockableConfigs.contains(it.name)) {
      resolutionStrategy.activateDependencyLocking()
    }
  }

  tasks.register("resolveAndLockAll") {
    notCompatibleWithConfigurationCache("Filters configurations at execution time")
    doFirst {
      assert gradle.startParameter.writeDependencyLocks :
          "$path must be run from the command line with the `--write-locks` flag"
    }
    doLast {
      configurations.findAll {
        // Add any custom filtering on the configurations to be resolved
        it.canBeResolved
      }.each { it.resolve() }
    }
  }
}
