/*
 * Copyright Â© 2018-2019, 2021 Apple Inc. and the ServiceTalk project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "io.servicetalk.servicetalk-gradle-plugin-internal-library"

dependencies {
  api project(":servicetalk-buffer-api")
  api project(":servicetalk-client-api")
  api project(":servicetalk-concurrent")
  api project(":servicetalk-concurrent-api")
  api project(":servicetalk-context-api")
  api project(":servicetalk-encoding-api")
  api project(":servicetalk-logging-api")
  api project(":servicetalk-oio-api")
  api project(":servicetalk-serialization-api")
  api project(":servicetalk-serializer-api")
  api project(":servicetalk-transport-api")

  implementation project(":servicetalk-annotations")
  implementation project(":servicetalk-concurrent-internal")
  implementation project(":servicetalk-concurrent-api-internal")
  implementation project(":servicetalk-encoding-api-internal")
  implementation project(":servicetalk-serializer-utils")
  implementation project(":servicetalk-utils-internal")
  implementation project(":servicetalk-oio-api-internal")
  implementation "org.slf4j:slf4j-api:$slf4jVersion"

  testImplementation enforcedPlatform("io.netty:netty-bom:$nettyVersion")
  testImplementation enforcedPlatform("org.junit:junit-bom:$junit5Version")
  testImplementation testFixtures(project(":servicetalk-buffer-api"))
  testImplementation testFixtures(project(":servicetalk-concurrent-api"))
  testImplementation testFixtures(project(":servicetalk-concurrent-internal"))
  testImplementation testFixtures(project(":servicetalk-transport-netty-internal"))
  testImplementation project(":servicetalk-buffer-netty")
  testImplementation project(":servicetalk-concurrent-test-internal")
  testImplementation project(":servicetalk-encoding-netty")
  testImplementation project(":servicetalk-test-resources")
  testImplementation project(":servicetalk-transport-netty-internal")
  testImplementation "io.netty:netty-codec"
  testImplementation "io.netty:netty-common"
  testImplementation "org.junit.jupiter:junit-jupiter-api"
  testImplementation "org.junit.jupiter:junit-jupiter-params"
  testImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
  testImplementation "org.mockito:mockito-core:$mockitoCoreVersion"
  testImplementation "org.mockito:mockito-junit-jupiter:$mockitoCoreVersion"

  testFixturesApi project(":servicetalk-buffer-api")
  testFixturesApi project(":servicetalk-concurrent-api")
  testFixturesApi project(":servicetalk-transport-api")

  testFixturesImplementation platform("org.junit:junit-bom:$junit5Version")
  testFixturesImplementation testFixtures(project(":servicetalk-transport-netty-internal"))
  testFixturesImplementation project(":servicetalk-buffer-netty")
  testFixturesImplementation "com.google.code.findbugs:jsr305:$jsr305Version"
  testFixturesImplementation "org.junit.jupiter:junit-jupiter-api"
  testFixturesImplementation "org.junit.jupiter:junit-jupiter-params"
  testFixturesImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
  testFixturesImplementation "org.mockito:mockito-core:$mockitoCoreVersion"
  testFixturesImplementation "org.mockito:mockito-junit-jupiter:$mockitoCoreVersion"
}

sourceSets {
  testProps {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDirs("src/test")
      include "**/DefaultHttpSetCookiesTest.java"
      include "**/DefaultHttpSetCookiesRfc6265Test.java"
      include "**/LegacyCookieParsingTest.java"
    }
  }
}

configurations {
  testPropsImplementation.extendsFrom testImplementation
  testPropsRuntimeOnly.extendsFrom testRuntimeOnly
}

task testProps(type: Test) {
  useJUnitPlatform()
  testClassesDirs = sourceSets.testProps.output.classesDirs
  classpath = sourceSets.testProps.runtimeClasspath
  systemProperty 'io.servicetalk.http.api.headers.cookieParsingStrictRfc6265', 'true'
}
test.dependsOn testProps
// ServiceTalkLibraryPlugin adds a spotbugs task for each sourceSet, we don't need it.
spotbugsTestProps.enabled = false
