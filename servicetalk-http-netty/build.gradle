/*
 * Copyright Â© 2018-2021 Apple Inc. and the ServiceTalk project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "io.servicetalk.servicetalk-gradle-plugin-internal-library"

dependencies {
  implementation platform(project(":servicetalk-dependencies"))
  testImplementation enforcedPlatform(project(":servicetalk-dependencies"))
  testImplementation enforcedPlatform("org.junit:junit-bom:$junit5Version")
  testFixturesImplementation platform(project(":servicetalk-dependencies"))

  api project(":servicetalk-concurrent-api")
  api project(":servicetalk-logging-api")
  api project(":servicetalk-http-api")

  implementation project(":servicetalk-annotations")
  implementation project(":servicetalk-client-api-internal")
  implementation project(":servicetalk-concurrent-internal")
  implementation project(":servicetalk-concurrent-api-internal")
  implementation project(":servicetalk-dns-discovery-netty")
  implementation project(":servicetalk-http-utils")
  implementation project(":servicetalk-loadbalancer")
  implementation project(":servicetalk-loadbalancer-experimental")
  implementation project(":servicetalk-logging-slf4j-internal")
  implementation project(":servicetalk-tcp-netty-internal")
  implementation project(":servicetalk-transport-netty")
  implementation project(":servicetalk-transport-netty-internal")
  implementation project(":servicetalk-utils-internal")
  implementation "com.google.code.findbugs:jsr305"
  implementation "io.netty:netty-codec-http"
  implementation "io.netty:netty-codec-http2"
  implementation "org.slf4j:slf4j-api"

  testImplementation testFixtures(project(":servicetalk-buffer-api"))
  testImplementation testFixtures(project(":servicetalk-concurrent-api"))
  testImplementation testFixtures(project(":servicetalk-concurrent-internal"))
  testImplementation testFixtures(project(":servicetalk-concurrent-reactivestreams"))
  testImplementation testFixtures(project(":servicetalk-http-api"))
  testImplementation testFixtures(project(":servicetalk-log4j2-mdc-utils"))
  testImplementation testFixtures(project(":servicetalk-transport-netty-internal"))
  testImplementation project(":servicetalk-concurrent-api-test")
  testImplementation project(":servicetalk-concurrent-test-internal")
  testImplementation project(":servicetalk-data-jackson")
  testImplementation project(":servicetalk-encoding-api-internal")
  testImplementation project(":servicetalk-encoding-netty")
  testImplementation project(":servicetalk-test-resources")
  testImplementation project(":servicetalk-utils-internal")
  testImplementation project(":servicetalk-oio-api-internal")
  testImplementation "io.netty:netty-transport-native-unix-common"
  testImplementation "io.netty.incubator:netty-incubator-transport-native-io_uring"
  testRuntimeOnly( group:"io.netty.incubator", name:"netty-incubator-transport-native-io_uring", classifier:"linux-x86_64")
  testRuntimeOnly( group:"io.netty.incubator", name:"netty-incubator-transport-native-io_uring", classifier:"linux-aarch_64")
  testImplementation "org.junit.jupiter:junit-jupiter-api"
  testImplementation "org.junit.jupiter:junit-jupiter-params"
  testImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
  testImplementation "org.mockito:mockito-core:$mockitoCoreVersion"
  testImplementation "org.mockito:mockito-junit-jupiter:$mockitoCoreVersion"
  testImplementation "org.testng:testng:$testngVersion"

  testFixturesImplementation testFixtures(project(":servicetalk-transport-netty-internal"))
  testFixturesImplementation project(":servicetalk-http-utils")
  testFixturesImplementation project(":servicetalk-test-resources")
  testFixturesImplementation "com.google.code.findbugs:jsr305"
  testFixturesImplementation "org.slf4j:slf4j-api"
  testFixturesImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
  testFixturesRuntimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
}

// We need testng for the reactive-streams-tck
task tck(type: Test) {
  useTestNG()
}
test.dependsOn tck

sourceSets {
  testProps {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDirs("src/test")
      include "**/H2PriorKnowledgeFeatureParityTest.java"
    }
  }
}

configurations {
  testPropsImplementation.extendsFrom testImplementation
  testPropsRuntimeOnly.extendsFrom testRuntimeOnly
}

task testProps(type: Test) {
  useJUnitPlatform()
  testClassesDirs = sourceSets.testProps.output.classesDirs
  classpath = sourceSets.testProps.runtimeClasspath
  systemProperty 'io.servicetalk.http.api.headers.cookieParsingStrictRfc6265', 'true'
}
test.dependsOn testProps
// ServiceTalkLibraryPlugin adds a spotbugs task for each sourceSet, we don't need it.
spotbugsTestProps.enabled = false
